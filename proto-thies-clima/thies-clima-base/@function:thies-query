function thies_query(id as long, cmd as string, param as long) as string * 2233
    '' NOTE: param = -1 omits parameter
    dim command as string * 10, results as string * 2233, trailer as string * 2, num as long
    select case param
    case -1         : sprintf(command, "%02d%s", id, uppercase(cmd))
    case 0 to 99999 : sprintf(command, "%02d%s%05d", id, uppercase(cmd), param)
    case else       : return QUESTION & QUESTION & "invalid parameter: " & param
    endselect
    serialOut({{COMMPORT:Com1}}, CR & command & CR,"",0,0)
    serialin(results,{{COMMPORT:Com1}},300,CR,2233)
    select case left(results,1)
    case BANG
        '' examples: "!00BR00005", "!00CI00002"
        select case mid(results,4,2)
        case uppercase(cmd)
            num = mid(results,6,5)
            return num
        case "CE"
            select case right(results,1)
            case 4  : results = QUESTION & results & "U3D: parameter violation"    '' !00CE00004
            case 8  : results = QUESTION & results & "U3D: incorrect access mode"  '' !00CE00008
            case 16 : results = QUESTION & results & "U3D: parameter out of range" '' !00CE00016
            case 32 : results = QUESTION & results & "U3D: parameter violation"    '' !00CE00032
            endselect
        case "CI"
            select case right(results,1)
            case 0  : results = QUESTION & results & "LPM: invalid parameter"      '' !00CI00000
            case 2  : results = QUESTION & results & "LPM: unknown command"        '' !00CI00002
            case 4  : results = QUESTION & results & "LPM: parameter out of range" '' !00CI00004
            case 8  : results = QUESTION & results & "LPM: config mode required"   '' !00CI00008
            endselect
        case else
            results = QUESTION & QUESTION & "unexpected:" & results
        endselect
    case STX
        serialIn(trailer,{{COMMPORT:Com1}},30,ETX,2)
        results &= trailer
    case "U","W"
        if uppercase(cmd) = "KY" and (results = "USER ACCESS" or  results = "WRITE PROTECTED") then
            serialin(results,{{COMMPORT:Com1}},300,CR,2233)
            if left(results,1) = BANG then
                num = mid(results,6,5)
                return num
            endif
        endif
    endselect
    return results
endfunction
